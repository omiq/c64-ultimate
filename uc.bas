10 REM ------------------------------------------------------------
20 REM SWIFTLINK TCP TERMINAL (C64 BASIC V2)
30 REM USES DIRECT PEEK/POKE TO THE SWIFTLINK 6551 ACIA
40 REM ACIA REGISTER ADDRESSES (SWIFTLINK AT $DE00)
45 REM ------------------------------------------------------------
50 TG$="":TT=0 :REM FLAG FOR TRACKING HTML TAGS (1 = INSIDE TAG, 0 = OUTSIDE)
60 DR=56832 :REM DATA REGISTER (READ = RECEIVE, WRITE = TRANSMIT)
70 SR=56833 :REM STATUS REGISTER (FLAGS: RX READY, TX READY, ERRORS)
80 CM=56834 :REM COMMAND REGISTER (PARITY, ECHO, DTR, INTERRUPTS)
90 CT=56835 :REM CONTROL REGISTER (BAUD RATE, WORD SIZE, STOP BITS)
100 REM CLEAR SCREEN AND SET TEXT COLOUR
110 PRINT CHR$(142);CHR$(147);CHR$(5);"CONNECTING ...":S=0
120 REM RESET ACIA BY WRITING TO STATUS REGISTER
130 POKE SR,0
140 REM CONTROL REGISTER
150 REM 31 = 8 DATA BITS, 1 STOP BIT, INTERNAL CLOCK,
160 REM BAUD SETTING DOUBLED BY SWIFTLINK CRYSTAL = 38,400
170 POKE CT,31:REM POKE CT,31 ON C64U AND 16 ON VICE
180 REM COMMAND REGISTER
190 REM NO PARITY, NO ECHO, DTR ON, RECEIVE ENABLED
200 POKE CM,9
210 REM SHORT DELAY TO LET HARDWARE SETTLE
220 FOR I=1 TO 500:NEXT
245 FOR I=1 TO 3
250 B=ASC("+"):GOSUB 850
255 NEXT
270 REM DIAL TCP SERVER (IP:PORT)
280 TS$="ATDT PHP.RETROGAMECODERS.COM:80"+CHR$(13)
290 GOSUB 700
300 CRLF$=CHR$(13)+CHR$(10)
310 TS$="GET / HTTP/1.1"+CRLF$+"HOST: PHP.RETROGAMECODERS.COM"+CRLF$+"CONNECTION: CLOSE"+CRLF$+CRLF$
320 GOSUB 700
390 HT=0: REM FLAG FOR IF HTML STARTED (1 = STARTED, 0 = NOT STARTED)
400 REM ------------------------------------------------------------
410 REM MAIN LOOP TO READ AND PROCESS INCOMING DATA
420 REM ------------------------------------------------------------
440 S=PEEK(SR)
450 IF (S AND 8)=0 THEN GOTO 440
455 C=PEEK(DR)
460 IF C>=97 AND C<=122 THEN C=C-32
465 IF C=ASC("<") THEN TT=1: TG$="": GOTO 440
470 IF C=ASC(">") THEN TT=0: GOTO 440
475 IF TT=1 THEN TG$=TG$+CHR$(C)
480 IF TT=0 AND TG$<>"" THEN GOSUB 1000
585 IF TT=0 AND TG$="" THEN PRINT CHR$(C);
590 GOTO 440
600 END
700 REM ------------------------------------------------------------
710 REM SEND STRING TS$ CHARACTER BY CHARACTER
720 REM ------------------------------------------------------------
730 FOR I=1 TO LEN(TS$)
740 B=ASC(MID$(TS$,I,1))
750 GOSUB 800
760 NEXT
770 RETURN
800 REM ------------------------------------------------------------
810 REM SEND ONE BYTE IN B
820 REM WAIT UNTIL TRANSMIT REGISTER IS EMPTY
830 REM BIT 4 (VALUE 16) = TRANSMIT READY
840 REM ------------------------------------------------------------
850 S=PEEK(SR)
860 IF (S AND 16)=0 THEN 850
870 POKE DR,B
880 RETURN
1000 REM ------------------------------------------------------------
1010 REM PROCESS HTML TAG IN TG$
1020 REM ------------------------------------------------------------
1030 IF TG$="HTML" THEN PRINT "{CLR}";
1040 IF TG$="H1" THEN PRINT "{RVS ON}";
1050 IF TG$="/H1" THEN PRINT "{RVS OFF}";CHR$(13);
1060 REM PRINT TG$
2000 TG$="": RETURN
