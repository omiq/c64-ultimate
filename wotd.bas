W$="OK##DISHEVELED#A DISHEVELED PERSON OR THING IS NOT NEAT OR TIDY.#"
10 REM ------------------------------------------------------------
20 REM SWIFTLINK TCP TERMINAL (C64 BASIC V2)
30 REM USES DIRECT PEEK/POKE TO THE SWIFTLINK 6551 ACIA
40 REM ------------------------------------------------------------
50 REM ACIA REGISTER ADDRESSES (SWIFTLINK AT $DE00)
60 DR=56832 :REM DATA REGISTER (READ = RECEIVE, WRITE = TRANSMIT)
70 SR=56833 :REM STATUS REGISTER (FLAGS: RX READY, TX READY, ERRORS)
80 CM=56834 :REM COMMAND REGISTER (PARITY, ECHO, DTR, INTERRUPTS)
90 CT=56835 :REM CONTROL REGISTER (BAUD RATE, WORD SIZE, STOP BITS)
100 REM CLEAR SCREEN AND SET TEXT COLOUR
110 PRINT CHR$(147);CHR$(5);:S=0
120 REM RESET ACIA BY WRITING TO STATUS REGISTER
130 POKE SR,0
140 REM CONTROL REGISTER
150 REM 31 = 8 DATA BITS, 1 STOP BIT, INTERNAL CLOCK,
160 REM 19,200 BAUD SETTING DOUBLED BY SWIFTLINK CRYSTAL = 38,400
170 POKE CT,18:REM POKE CT,31 ON C64U
180 REM COMMAND REGISTER
190 REM NO PARITY, NO ECHO, DTR ON, RECEIVE ENABLED
200 POKE CM,9
210 REM SHORT DELAY TO LET HARDWARE SETTLE
220 FOR I=1 TO 500:NEXT
230 REM SEND "AT"TO WAKE UP TCP MODEM EMULATION
240 REM LEADING CR ENSURES CLEAN COMMAND STATE
250 TS$=CHR$(13)+"AT"+CHR$(13)
260 GOSUB 700
270 REM DIAL TCP SERVER (IP:PORT)
280 TS$="ATDT 127.0.0.1:6464"+CHR$(13)+CHR$(13)+CHR$(13)
290 GOSUB 700
300 T$=""
320 REM ------------------------------------------------------------
330 REM MAIN LOOP
440 S=PEEK(SR)
450 IF (S AND 8)=0 THEN GOTO 440
470 C=PEEK(DR)
500 IF C<>35 THEN GOTO 440
510 S=PEEK(SR)
520 IF (S AND 8)=0 THEN GOTO 510
530 C=PEEK(DR)
540 IF C<>13 THEN I$=I$+CHR$(C)
550 IF C=35 THEN PRINT I$;:I$="":S=S+1
690 GOTO 510
700 REM ------------------------------------------------------------
710 REM SEND STRING TS$ CHARACTER BY CHARACTER
720 REM ------------------------------------------------------------
730 FOR I=1 TO LEN(TS$)
740 B=ASC(MID$(TS$,I,1))
750 GOSUB 800
760 NEXT
770 RETURN
800 REM ------------------------------------------------------------
810 REM SEND ONE BYTE IN B
820 REM WAIT UNTIL TRANSMIT REGISTER IS EMPTY
830 REM BIT 4 (VALUE 16) = TRANSMIT READY
840 REM ------------------------------------------------------------
850 S=PEEK(SR)
860 IF (S AND 16)=0 THEN 850
870 POKE DR,B
880 RETURN
